
load "../stzbase.ring"

/*--- Softanza typical code for programming reactive streams

# First, everything must happen inside a ReactiveSystem object.
# In Softanza, reactive streams (and any other reactive element)
# cannot exist in a vacuum!

# Create a Reactive System instance
Rs = new stzReactiveSystem()
Rs {
    # Inside the reactive system (which is actually built on the
    # Libuv infrastructure), we create a reactive stream object to
    # work with, and begin defining it.

    oPriceStream = CreateStreamXT("my-price-api", OPTIMISED_FOR_NETWORK_SOURCE)
    # The stream is defined with type :NETWORK, telling the libuv engine
    # to prepare for receiving data from the network (e.g., over HTTP).

    # Declarative code that defines the reactive stream
    # (it will not run until Rs.RunLoop() is reached at the end).

    # When data items arrive in this pipeline called oPriceStream:
    oPriceStream {

        # Transform each incoming item using this action (a business rule).
        Transform(func price { return price * 1.20 })

        # Then filter the transformed values to keep only those
        # that satisfy this condition.
        Filter(func price { return price >= 120 })

        # If the transformed value successfully passes the filter,
        # its journey in the pipeline concludes with this final action.
 
        OnPassed(func item { SaveToDatabase(item) NotifyUsersAbout(item) })

        # Then, take the next item in the pipeline and repeat the same process.
        # Ideally, all items will be transformed, filtered, and concluded properly,
        # until no more data is available (the pipeline runs dry). At that point,
        # we can gracefully say goodbye using this action.

        OnNoMore(func() { CloseConnections() SendSummary() })

        # If, however, an error occurs at any stage,
        # this action ensures the reactive system remains under control.
        # The reactive loop (and data pipeline) is then interrupted
        # responsibly with a clear recovery plan.

        OnError(func error { Log(error) AlertTeamAbout(error) })

	# The last step is to define the data the stream will Recieve.
	# Here we provide test data, with duplicates and varying price ranges.
        # Test data: values below 100 will be filtered out after transformation (< 120)
        # Values 100+ will pass through after transformation (>= 120)

    	anTestPrices = [ 80, 90, 95, 100, 120, 150, 200 ]
	RecieveMany(anTestPrices)

   }

    # Now we leave the static mindset above and fire up the reactive loop.
    # This engages the libuv loop systemâ€”the engine and "dark magic"
    # that brings the system to life, accepts data, reacts according to
    # our defined strategy, and produces results through its internal
    # graph of computation.

     RunLoop()
}

pf()

func SaveToDatabase(pItem)
	? "Saving item (" + @@(pItem) + ") to the database..."

func NotifyUsersAbout(pItem)
	? "Notifying users about item (" + @@(pItem) + ")..." + NL

func CloseConnections()
	? "Closing all connections..."

func SendSummary()
	? "Sending summary of the operation..."

func Log(pcError)
	? "Logging error: " + pcError + "."

func AlertTeamAbout(pcError)
	? "Alerting team about error: " + pcError + "."

#-->
# Saving item (120) to the database...
# Notifying users about item (120)...
# 
# Saving item (144) to the database...
# Notifying users about item (144)...
# 
# Saving item (180) to the database...
# Notifying users about item (180)...
# 
# Saving item (240) to the database...
# Notifying users about item (240)...


# Executed in 5.74 second(s) in Ring 1.23

/*--- Basic Stream Operations

# Demonstrates fundamental stream creation, Recieveing, and lifecycle
# Essential concepts: Recieve, OnPassed, Error handling, Completion

pr()

Rs = new stzReactiveSystem()
Rs {

    # Create a manual stream for basic operations
    oBasicStream = CreateStream("basic-example")
    oBasicStream {

        # Each item of data that the stream is fed with
	# is processed using this Rfunction
        OnPassed(func data {
            ? "ğŸ“Š Data Recieved: " + data
        })

        # Potential erros are captured by the fellowing Rfunction
        OnError(func error {
            ? "âŒ Error occurred: " + error
        })

        # When there are no more data items to process,
	# this Rfunction is exuecuted

        OnNoMore(func() {
            ? "âœ… Stream processing completed"
        })

        # Recieve individual data points
        Recieve("First message")
        Recieve("Second message")

        # Recieve the stream with multiple items at once
        RecieveMany(["Third", "Fourth", "Fifth"])

        # Check that error handling works (when stream errors don't work)
        CheckErrorHandling("Simulated error")
        # Note: Stream automatically stops after error
    }

    RunLoop()
    #-->
    # ğŸ“Š Data Recieved: First message
    # ğŸ“Š Data Recieved: Second message  
    # ğŸ“Š Data Recieved: Third
    # ğŸ“Š Data Recieved: Fourth
    # ğŸ“Š Data Recieved: Fifth
    # âŒ Error occurred: Simulated error
}

pf()
# Executed in 0.93 second(s) in Ring 1.23

/*--- Stream Transformations - Map, Filter

# Shows data transformation capabilities with chaining
# Key concepts: Map (transform), Filter (predicate)

pr()

Rs = new stzReactiveSystem()
Rs {
    
    # Create stream for data processing pipeline
    oTransformStream = CreateStream("transform-pipeline")
    oTransformStream {
        # Chain transformations
        Transform(func price { return price * 1.20 })		# Add 20% tax
        Filter(func price { return price >= 100 })	# Only expensive items

        OnPassed(func finalPrice {
            ? "ğŸ’° Final price after tax & filtering: $" + finalPrice
        })

        # Test data with duplicates and various price ranges
        testPrices = [ 80, 90, 100, 120, 150, 200 ]
        RecieveMany(testPrices)

    }

    RunLoop()
    #-->
    # ğŸ’° Final price after tax & filtering: $108
    # ğŸ’° Final price after tax & filtering: $120  
    # ğŸ’° Final price after tax & filtering: $144
    # ğŸ’° Final price after tax & filtering: $180
    # ğŸ’° Final price after tax & filtering: $240
}

pf()
# Executed in 0.93 second(s) in Ring 1.23


/*--- Data Aggregation with Accumulate

# Shows how streams manage accumulated data that must be explicitly concluded
# Essential for analytics, totals, and summary operations

pr()

Rs = new stzReactiveSystem()
Rs {

    # Sales data aggregation stream

    oSalesStream = CreateStream("sales-aggregation")
    oSalesStream {

        Accumulate( func(total, sale) {
            return total + sale["amount"]  # Running total kept internally
        }, 0)  # Starting value: $0.00


        OnPassed( func totalSales {
            ? "ğŸ’° Total Sales: $" + totalSales
        })


        OnNoMore( func() {
            ? "âœ… Sales calculation completed"
        })

        # Daily sales transactions
        salesData = [
            [ :amount = 150.00, :product = "Laptop" ],
            [ :amount = 89.99, :product = "Mouse" ], 
            [ :amount = 299.99, :product = "Monitor" ],
            [ :amount = 45.50, :product = "Keyboard" ]
        ]

        # Recieve all sales data into the accumulation pipeline
        # Each item gets added to the internal accumulator (150 + 89.99 + 299.99 + 45.50)
 
        RecieveMany(salesData)

    }

    RunLoop()
    #-->
    # ğŸ’° Total Sales: $585.48
    # âœ… Sales calculation completed
}
# Executed in 0.95 second(s) in Ring 1.23


pf()
# Executed in 0.95 second(s) in Ring 1.23

/*--- Analytics Dashboard with Multiple Metrics

# Complex accumulate example calculating multiple statistics
# Shows real-world analytics use case

pr()

Rs = new stzReactiveSystem()
Rs {
    # Website analytics stream

    oAnalyticsStream = CreateStream("web-analytics")
    oAnalyticsStream {

        # Aggregate user session data

        Accumulate(func(stats, session) {
            stats[:totalUsers] += 1
            stats[:totalPageViews] += session[:pageViews]
            stats[:totalSessionTime] += session[:duration]
            
            if session[:converted]
                stats[:conversions] += 1
            ok
            
            return stats

            },
	    [
		:totalUsers = 0,
		:totalPageViews = 0,
		:totalSessionTime = 0,
		:conversions = 0
	    ])

        OnPassed(func analytics {
            avgPageViews = analytics[:totalPageViews] / analytics[:totalUsers]
            avgSessionTime = analytics[:totalSessionTime] / analytics[:totalUsers]
            conversionRate = (analytics[:conversions] * 100.0) / analytics[:totalUsers]
            
            ? "ğŸ“Š Website Analytics Summary:"
            ? "   Total Users: " + analytics[:totalUsers]
            ? "   Avg Page Views: " + avgPageViews + " pages/user"
            ? "   Avg Session Time: " + avgSessionTime + " minutes"
            ? "   Conversion Rate: " + conversionRate + "%"
        })

        # User session data
        sessions = [
            [:pageViews = 5, :duration = 12.5, :converted = true],
            [:pageViews = 3, :duration = 8.2, :converted = false],
            [:pageViews = 7, :duration = 18.7, :converted = true], 
            [:pageViews = 2, :duration = 4.1, :converted = false],
            [:pageViews = 9, :duration = 25.3, :converted = true]
        ]

        RecieveMany(sessions)

    }
    
    RunReactiveLoop()
    #-->
    # ğŸ“Š Website Analytics Summary:
    #    Total Users: 5
    #    Avg Page Views: 5.2 pages/user
    #    Avg Session Time: 13.76 minutes
    #    Conversion Rate: 60%
}

pf()

/*--- Complex Transformation Chains

# Shows combining multiple transformation types
# Real-world scenario: Processing sensor data

pr()

Rs = new stzReactiveSystem()
Rs {
    
    # Sensor data processing stream
    oSensorStream = CreateStreamXT("sensor-data", OPTIMISED_FOR_SENSOR_SOURCE)
    oSensorStream {

        # Multi-stage processing pipeline
        Transform(func reading {
            # Convert raw sensor value to temperature
            return (reading - 32) * 5/9  # Fahrenheit to Celsius
        })

        Filter(func temp {
            # Only valid temperature readings
            return temp >= -50 and temp <= 100
        })

        Transform(func temp {
            # Round to 1 decimal place
            return floor(temp * 10) / 10
        })
     
        OnPassed(func temperature {
            alert = ""
            if temperature > 35
                alert = " ğŸ”¥ HIGH"
            but temperature < 0
                alert = " â„ï¸ FREEZING"
            ok
            ? "ğŸŒ¡ï¸  Temperature: " + temperature + "Â°C" + alert
        })
        
        # Simulate sensor readings (Fahrenheit)
        rawReadings = [ 68, 75, 32, 100, 212, -40, 150 ]
        RecieveMany(rawReadings)

    }
    
    RunLoop()
    #-->
    # ğŸŒ¡ï¸  Temperature: 20Â°C
    # ğŸŒ¡ï¸  Temperature: 23.8Â°C
    # ğŸŒ¡ï¸  Temperature: 0.0Â°C â„ï¸ FREEZING
    # ğŸŒ¡ï¸  Temperature: 37.7Â°C ğŸ”¥ HIGH
    # ğŸŒ¡ï¸  Temperature: 100.0Â°C ğŸ”¥ HIGH
    # ğŸŒ¡ï¸  Temperature: -40.0Â°C â„ï¸ FREEZING
    # ğŸŒ¡ï¸  Temperature: 65.5Â°C ğŸ”¥ HIGH
}

pf()
# Executed in 0.94 second(s) in Ring 1.23

/*--- Chat Message Processing

# Realistic example: Processing chat messages with filtering and
# formatting demonstrates practical stream usage

pr()

Rs = new stzReactiveSystem()
Rs {
    
    # Chat message processing pipeline

    oChatStream = CreateStream("chat-messages")
    oChatStream {

        # Filter out spam and inappropriate content

        Filter(func message {
            spamWords = [ "spam", "advertisement", "buy now" ]
            text = lower(message)
            
            nLenSpam = len(spamWords)
            for i = 1 to nLenSpam
                if substr(text, spamWords[i])
                    return false  # Block spam
                ok
            next
            
            return len(message) > 0 and len(message) <= 280  # Valid length
        })
        
        # Format messages

        Transform(func message {
            timestamp = "12:34"  # In real app, use actual timestamp
            return "[" + timestamp + "] " + message
        })
        
        OnPassed(func formattedMessage {
            ? "ğŸ’¬ " + formattedMessage
        })
        
        # Simulate chat messages

        messages = [
            "Hello everyone!",
            "How's everyone doing?", 
            "Check out this spam advertisement",  # Will be filtered
            "",  # Empty message - filtered
            "Great discussion today!",
            "See you later!"
        ]
        
        RecieveMany(messages)

    }
    
    RunLoop()
    #-->
    # ğŸ’¬ [12:34] Hello everyone!
    # ğŸ’¬ [12:34] How's everyone doing?
    # ğŸ’¬ [12:34] Great discussion today!
    # ğŸ’¬ [12:34] See you later!
}

pf()
# Executed in 0.94 second(s) in Ring 1.23

/*--- E-commerce Order Processing

# Complex business logic example with multiple transformations
# Shows stream composition for real business scenarios

pr()

Rs = new stzReactiveSystem()
Rs {
    
    # Order processing stream

    oOrderStream = CreateStream("order-processing")
    oOrderStream {

        # Validate orders

        Filter(func order {
            # Basic validation
            return order["amount"] > 0 and len(order["customer"]) > 0
        })
        
        # Calculate totals with tax and shipping

        Transform(func order {
            subtotal = order["amount"]
            tax = subtotal * 0.08  # 8% tax
            shipping = 15.00  # Flat rate shipping
            
            order["tax"] = tax
            order["shipping"] = shipping  
            order["total"] = subtotal + tax + shipping
            
            return order
        })
        
        # Filter high-value orders for special handling

        Filter(func order {
            return order["total"] >= 100  # VIP orders only
        })
        
        OnPassed(func processedOrder {
            ? "ğŸ›’ VIP Order processed:"
            ? "   Customer: " + processedOrder["customer"]
            ? "   Subtotal: $" + processedOrder["amount"]
            ? "   Tax: $" + processedOrder["tax"]  
            ? "   Shipping: $" + processedOrder["shipping"]
            ? "   Total: $" + processedOrder["total"]
            ? "   Status: Ready for priority shipping"
            ? ""
        })
        
        # Simulate incoming orders

        orders = [
            [ :customer = "John Doe", :amount = 89.99 ],      # Below threshold
            [ :customer = "Jane Smith", :amount = 150.00 ],   # VIP order
            [ :customer = "", :amount = 200.00 ],             # Invalid customer
            [ :customer = "Bob Wilson", :amount = 299.99 ],   # VIP order
            [ :customer = "Alice Brown", :amount = 0 ]        # Invalid amount
        ]
        
        RecieveMany(orders)

    }
    
    RunLoop()
    #-->
    '
	ğŸ›’ VIP Order processed:
	   Customer: John Doe
	   Subtotal: $89.99
	   Tax: $7.20
	   Shipping: $15
	   Total: $112.19
	   Status: Ready for priority shipping
	
	ğŸ›’ VIP Order processed:
	   Customer: Jane Smith
	   Subtotal: $150
	   Tax: $12
	   Shipping: $15
	   Total: $177
	   Status: Ready for priority shipping
	
	ğŸ›’ VIP Order processed:
	   Customer: Bob Wilson
	   Subtotal: $299.99
	   Tax: $24.00
	   Shipping: $15
	   Total: $338.99
	   Status: Ready for priority shipping
    '
}

pf()
# Executed in 0.93 second(s) in Ring 1.23

/*--- Timer-Based Streams

# Periodic data generation for monitoring, logging, scheduled tasks
# Essential for: Heartbeats, polling, periodic updates

pr()

Rs = new stzReactiveSystem()
Rs {

    # System monitoring with timer-based stream

    oTimerStream = CreateStreamXT("system-monitor", OPTIMISED_FOR_TIMER_SOURCE)
    oTimerStream {

        Transform(func tick {
            # Simulate system metrics collection
            return [
                :timestamp = clocksPerSecond(),
                :cpu_usage = random(100),
                :memory_usage = random(90) + 10,  # 10-100%
                :disk_space = random(50) + 50     # 50-100%
            ]
        })

        Filter(func metrics {
            # Alert on high resource usage
            return metrics[:cpu_usage] > 80 or metrics[:memory_usage] > 85
        })

        OnPassed(func alert {
            ? "âš ï¸ SYSTEM ALERT"
	    ? "----------------"
            ? " â€¢ CPU    : " + alert[:cpu_usage] + "%"
            ? " â€¢ Memory : " + alert[:memory_usage] + "%"
            ? " â€¢ Disk   : " + alert[:disk_space] + "%" + NL
        })

        OnNoMore(func() {
            ? NL + "âœ… Monitoring session ended"
        })

        # Simulate 5 timer ticks
        for i = 1 to 5
            Recieve(i)  # Timer tick simulation
        next

    }
    
    Run()
    #-->
    # âš ï¸ SYSTEM ALERT
    # ----------------
    #  â€¢ CPU    : 92%
    #  â€¢ Memory : 60%
    #  â€¢ Disk   : 88%
    # 
    # âš ï¸ SYSTEM ALERT
    # ----------------
    #  â€¢ CPU    : 94%
    #  â€¢ Memory : 19%
    #  â€¢ Disk   : 93%
    # 
    # âš ï¸ SYSTEM ALERT
    # ----------------
    #  â€¢ CPU    : 2%
    #  â€¢ Memory : 95%
    #  â€¢ Disk   : 100%
    # 
    # âœ… Monitoring session ended
}

#TODO Check why the number of alerts varies if we run the sample
# several times (1, 2, 3 or even nothing)

pf()
# Executed in 0.94 second(s) in Ring 1.23

/*--- File-Based Streams

# File monitoring, log processing, configuration watching
# Essential for: Log analysis, file system events, data ingestion

pr()

Rs = new stzReactiveSystem()
Rs {
    # Log file processing stream
    oFileStream = CreateStreamXT("log-processor", OPTIMISED_FOR_FILE_SOURCE)
    oFileStream {

        # Parse log entries
        Transform(func logLine {
            # Simulate log parsing
            parts = split(logLine, "|")
            if len(parts) >= 3
                return [
                    :timestamp = parts[1],
                    :level = parts[2], 
                    :message = parts[3]
                ]
            else
                return [:level = "INFO", :message = logLine]
            ok
        })
        
        # Filter critical events
        Filter(func logEntry {
            criticalLevels = ["ERROR", "CRITICAL", "FATAL"]
            return find(criticalLevels, upper(logEntry[:level]))
        })
        
        OnPassed(func criticalLog {
            ? "ğŸš¨ CRITICAL LOG EVENT"
            ? " â€¢ Level: " + criticalLog[:level]
            ? " â€¢ Message: " + criticalLog[:message] + NL
        })
        
        # Simulate log file content
        logLines = [
            "2024-01-15 10:30:15|INFO|User login successful",
            "2024-01-15 10:31:02|ERROR|Database connection failed",
            "2024-01-15 10:31:05|CRITICAL|System memory exhausted",
            "2024-01-15 10:32:10|INFO|Backup completed successfully",
            "2024-01-15 10:33:22|FATAL|Security breach detected"
        ]
        
        RecieveMany(logLines)

    }
    
    RunReactiveLoop()
    #-->
    # ğŸš¨ CRITICAL LOG EVENT
    # â€¢ Level: ERROR
    # â€¢ Message: Database connection failed
    # 
    # ğŸš¨ CRITICAL LOG EVENT
    # â€¢ Level: CRITICAL
    # â€¢ Message: System memory exhausted
    # 
    # ğŸš¨ CRITICAL LOG EVENT
    # â€¢ Level: FATAL
    # â€¢ Message: Security breach detected
}

pf()
# Executed in 0.95 second(s) in Ring 1.23

/*--- Network-Based Streams

# HTTP requests, WebSocket connections, API polling
# Essential for: Real-time data, API integration, network monitoring

pr()

Rs = new stzReactiveSystem()
Rs {
    # API data processing stream
    oNetworkStream = CreateStreamXT("api-monitor", OPTIMISED_FOR_NETWORK_SOURCE)
    oNetworkStream {
        # Parse API responses
        Transform(func apiResponse {
            # Simulate JSON parsing
            return [
                :endpoint = apiResponse[:url],
                :status_code = apiResponse[:status],
                :response_time = apiResponse[:time],
                :data_size = apiResponse[:size]
            ]
        })
        
        # Monitor performance issues
        Filter(func response {
            return response[:status_code] >= 400 or response[:response_time] > 2000
        })
        
        OnPassed(func issue {

            issueType = ""
            if issue[:status_code] >= 500
                issueType = "ğŸ”´ SERVER ERROR"

            but issue[:status_code] >= 400
                issueType = "ğŸŸ¡ CLIENT ERROR"  

            but issue[:response_time] > 2000
                issueType = "ğŸŒ SLOW RESPONSE"
            ok
            
            ? issueType
            ? "â€¢ Endpoint: " + issue[:endpoint]
            ? "â€¢ Status: " + issue[:status_code]
            ? "â€¢ Response Time: " + issue[:response_time] + "ms" + NL
        })
        
        # Simulate API responses
        responses = [
            [:url = "/api/users", :status = 200, :time = 150, :size = 1024],
            [:url = "/api/orders", :status = 404, :time = 89, :size = 256],
            [:url = "/api/products", :status = 500, :time = 3500, :size = 0],
            [:url = "/api/payments", :status = 200, :time = 2500, :size = 512]
        ]
        
        RecieveMany(responses)

    }
    
    RunLoop()
    #-->
    '
    ğŸŸ¡ CLIENT ERROR
    â€¢ Endpoint: /api/orders
    â€¢ Status: 404
    â€¢ Response Time: 89ms

    ğŸ”´ SERVER ERROR
    â€¢ Endpoint: /api/products
    â€¢ Status: 500
    â€¢ Response Time: 3500ms

    ğŸŒ SLOW RESPONSE
    â€¢ Endpoint: /api/payments
    â€¢ Status: 200
    â€¢ Response Time: 2500ms
    '
}

pf()
# Executed in 0.94 second(s) in Ring 1.23

/*--- Sensor-Based Streams

# IoT devices, environmental monitoring, real-time measurements
# Essential for: IoT applications, monitoring systems, data acquisition

pr()

Rs = new stzReactiveSystem()
Rs {
    # Environmental monitoring stream
    oSensorStream = CreateStreamXT("environment-monitor", OPTIMISED_FOR_SENSOR_SOURCE)
    oSensorStream {

        # Calibrate sensor readings
        Transform(func rawReading {
            # Simulate sensor data processing
            return [
                :sensor_id = rawReading[:id],
                :temperature = rawReading[:temp] * 0.1,  # Convert to Celsius
                :humidity = rawReading[:humidity],
                :air_quality = rawReading[:aqi],
                :timestamp = clocksPerSecond()
            ]
        })
        
        # Environmental alerts
        Filter(func reading {
            return reading[:temperature] > 30 or 
                   reading[:humidity] > 80 or
                   reading[:air_quality] > 150
        })
        
        OnPassed(func alert {
            alertTypes = []

            if alert[:temperature] > 30
                alertTypes + "ğŸŒ¡ï¸ HIGH TEMP"
            ok

            if alert[:humidity] > 80  
                alertTypes + "ğŸ’§ HIGH HUMIDITY"
            ok

            if alert[:air_quality] > 150
                alertTypes + "ğŸ­ POOR AIR QUALITY"
            ok
            
            ? "âš ï¸ ENVIRONMENTAL ALERT: " + JoinXT(alertTypes, " + ")
            ? "â€¢ Sensor: " + alert[:sensor_id] 
            ? "â€¢ Temperature: " + alert[:temperature] + "Â°C"
            ? "â€¢ Humidity: " + alert[:humidity] + "%"
            ? "â€¢ Air Quality Index: " + alert[:air_quality] + NL
        })
        
        # Simulate sensor readings
        sensorData = [
            [:id = "TEMP_01", :temp = 250, :humidity = 45, :aqi = 75],   # Normal
            [:id = "TEMP_02", :temp = 320, :humidity = 85, :aqi = 180],  # High temp + humidity + AQI
            [:id = "TEMP_03", :temp = 280, :humidity = 90, :aqi = 120],  # High humidity only
            [:id = "TEMP_04", :temp = 350, :humidity = 60, :aqi = 95]    # High temp only
        ]
        
        RecieveMany(sensorData)

    }
    
    RunLoop()
    #-->
    '
    âš ï¸ ENVIRONMENTAL ALERT: ğŸŒ¡ï¸ HIGH TEMP + ğŸ’§ HIGH HUMIDITY + ğŸ­ POOR AIR QUALITY
    â€¢ Sensor: TEMP_02
    â€¢ Temperature: 32Â°C
    â€¢ Humidity: 85%
    â€¢ Air Quality Index: 180

    âš ï¸ ENVIRONMENTAL ALERT: ğŸ’§ HIGH HUMIDITY
    â€¢ Sensor: TEMP_03
    â€¢ Temperature: 28Â°C
    â€¢ Humidity: 90%
    â€¢ Air Quality Index: 120

    âš ï¸ ENVIRONMENTAL ALERT: ğŸŒ¡ï¸ HIGH TEMP
    â€¢ Sensor: TEMP_04
    â€¢ Temperature: 35Â°C
    â€¢ Humidity: 60%
    â€¢ Air Quality Index: 95
    '
}

pf()
# Executed in 1.06 second(s) in Ring 1.23

/*--- LibUV Integration Example

# Low-level system integration using LibUV handles (called Rfunctions in Softanza)
# Essential for: High-performance I/O, system-level operations

pr()

Rs = new stzReactiveSystem()
Rs {
    # System process monitoring via LibUV
    oUVStream = CreateStreamXT("process-monitor", OPTIMISED_FOR_LIBUV_MESSAGES)
    oUVStream {

        # Process system events
        Transform(func uvEvent {
            # Simulate LibUV event processing
            return [
                :event_type = uvEvent[:type],
                :process_id = uvEvent[:pid],
                :resource_usage = uvEvent[:resources],
                :event_time = clocksPerSecond()
            ]
        })
        
        # Filter high-impact events
        Filter(func processEvent {
            highImpactEvents = ["process_crash", "memory_leak", "high_cpu"]
            return find(highImpactEvents, processEvent[:event_type])
        })
        
        OnPassed(func criticalEvent {
            ? "ğŸ”¥ SYSTEM EVENT DETECTED:"
            ? "â€¢ Type: " + criticalEvent[:event_type]
            ? "â€¢ Process ID: " + criticalEvent[:process_id] 
            ? "â€¢ Resource Impact: " + criticalEvent[:resource_usage] + "%" + NL
        })
        
        OnNoMore(func() {
            ? "ğŸ›¡ï¸ LibUV monitoring stopped - resources cleaned up"
        })
        
        # Simulate LibUV system events
        systemEvents = [
            [:type = "process_start", :pid = 1234, :resources = 15],
            [:type = "process_crash", :pid = 5678, :resources = 85],
            [:type = "normal_operation", :pid = 9012, :resources = 25],
            [:type = "memory_leak", :pid = 3456, :resources = 95],
            [:type = "high_cpu", :pid = 7890, :resources = 90]
        ]
        
        RecieveMany(systemEvents)

    }
    
    RunLoop()
    #-->
    '
    ğŸ”¥ SYSTEM EVENT DETECTED:
    â€¢ Type: process_crash
    â€¢ Process ID: 5678
    â€¢ Resource Impact: 85%

    ğŸ”¥ SYSTEM EVENT DETECTED:
    â€¢ Type: memory_leak
    â€¢ Process ID: 3456
    â€¢ Resource Impact: 95%

    ğŸ”¥ SYSTEM EVENT DETECTED:
    â€¢ Type: high_cpu
    â€¢ Process ID: 7890
    â€¢ Resource Impact: 90%

    ğŸ›¡ï¸ LibUV monitoring stopped - resources cleaned up
    '
}

pf()
# Executed in 0.93 second(s) in Ring 1.23

/*--- Multi-Source Stream Composition

# Combining different stream sources in complex applications
# Shows real-world architectural patterns

pr()

Rs = new stzReactiveSystem()
Rs {
    # Create multiple specialized streams
    
    # 1. User activity stream (manual)
    oUserStream = CreateStream("user-activity")
    oUserStream {
        Transform(func activity { 
            activity[:source] = "USER"
            return activity
        })
        OnPassed(func event { ? "ğŸ‘¤ " + event[:action] + " by " + event[:user] })
    }
    
    # 2. System health stream (timer)
    oHealthStream = CreateStreamXT("system-health", OPTIMISED_FOR_TIMER_SOURCE) 
    oHealthStream {
        Transform(func tick {
            return [:source = "SYSTEM", :status = "healthy", :load = random(100)]
        })
        Filter(func health { return health[:load] > 80 })
        OnPassed(func alert { ? "âš™ï¸  System load high: " + alert[:load] + "%" })
    }
    
    # 3. External API stream (network)
    oApiStream = CreateStreamXT("api-updates", :NETWORK)
    oApiStream {
        Transform(func response {
            response[:source] = "API"  
            return response
        })
        OnPassed(func update { ? "ğŸŒ API: " + update[:message] })
    }
    
    # Simulate multi-source events
    oUserStream.RecieveMany([
        [:action = "Login", :user = "alice"],
        [:action = "Purchase", :user = "bob"]
    ])
    
    # System health checks
    for i = 1 to 3
        oHealthStream.Recieve(i)
    next
    
    # API updates
    oApiStream.RecieveMany([
        [:message = "New feature deployed"],
        [:message = "Maintenance scheduled"]
    ])
    
    RunLoop()
    #-->
    # ğŸ‘¤ Login by alice
    # ğŸ‘¤ Purchase by bob
    # âš™ï¸  System load high: 94%
    # ğŸŒ API: New feature deployed
    # ğŸŒ API: Maintenance scheduled
}

pf()
# Executed in 0.92 second(s) in Ring 1.23

#==== SETTING STREAM AUTOCONCUDE ON AND OFF

/*--- Immediate Auto-Conclude (Default)
# Best for: Batch processing, complete datasets, sales reports

pr()

Rs = new stzReactiveSystem()
Rs {
    oSalesStream = CreateStream("batch-sales")
    oSalesStream {
        # Default: immediate auto-conclude after RecieveMany()
        SetAutoConclude(true)  # Default behavior
        SetAutoConcludeDelay(0)  # No delay - conclude immediately
        
        Accumulate(func(total, sale) {
            return total + sale["amount"]
        }, 0)
        
        OnPassed(func totalSales {
            ? "ğŸ’° Batch Total: $" + totalSales + " (concluded immediately)"
        })
        
        OnNoMore(func() {
            ? "âœ… Batch processing completed instantly"
        })
        
        # Complete dataset arrives at once
        salesData = [
            [:amount = 150.00, :product = "Laptop"],
            [:amount = 89.99, :product = "Mouse"], 
            [:amount = 299.99, :product = "Monitor"]
        ]
        
        RecieveMany(salesData)  # Auto-concludes immediately after processing
        # No manual Conclude() needed!
    }
}

pf()

/*--- Debounced Auto-Conclude 
# Best for: Streaming data, network feeds, sensor readings with gaps

pr()

Rs = new stzReactiveSystem()
Rs {
    oSensorStream = CreateStream("temperature-readings")
    oSensorStream {

        # Enable debounced auto-conclude - waits for data gaps
        # SetAutoConclude(true)
        # SetAutoConcludeDelay(500)  # Wait 500ms after last reading
        SetAutoConcludeXT(true, 500)

        Accumulate( func(avgTemp, reading) {
            # Simple moving average calculation
            if avgTemp = 0
                return reading["temp"]
            else
                return (avgTemp + reading["temp"]) / 2
            ok
        }, 0) # Starting from 0

        OnPassed(func finalAvg {
            ? "ğŸŒ¡ï¸  Final Average Temperature: " + finalAvg + "Â°C"
        })
 
        OnNoMore(func {
            ? "âœ… Sensor reading session completed (after 500ms delay)"
        })

       # Simulate streaming sensor data with gaps
        ? "ğŸ“¡ Receiving temperature readings..."

        # Schedule sensor readings using reactive timers instead of Sleep()
        Rs.SetTimeout(0, func {
            oSensorStream.Recieve([:temp = 22.5, :sensor = "A1"])
            oSensorStream.Recieve([:temp = 23.1, :sensor = "A2"])
        })

        Rs.SetTimeout(200, func {
            ? "   (200ms gap - still waiting...)"
            oSensorStream.Recieve([:temp = 21.8, :sensor = "A3"])
            oSensorStream.Recieve([:temp = 24.2, :sensor = "A4"])
        })

        Rs.SetTimeout(600, func {
            ? "   (600ms gap - will auto-conclude after 500ms...)"
            # No more data - stream will auto-conclude after 500ms delay
        })

        Rs.SetTimeOut(1200, func {
	  ? "   (System kept alive for auto-conclude)"
        })

        # Stream automatically concludes here due to 500ms timeout
        # No manual Conclude() needed!
    }

    RunLoop()

    ? ""
    ? "ğŸ’¡ Key Difference:"
    ? "   â€¢ Immediate: Perfect for complete datasets (sales batches)"  
    ? "   â€¢ Debounced: Handles streaming data with natural gaps (sensors, APIs)"
}

pf()
# Executed in 2.17 second(s) in Ring 1.23

#---------------------------------------------#
#  Overflow (backpresuure) Strategy Examples  #
#---------------------------------------------#
*/

#NOTE

# Overflow occurs when data arrives faster than it can be processed.
# The stream Recieves data (from producers) faster than Rfunctions
# (subscribers) can handle it, so the system applies overflow
# strategies to manage the excess flow.

/*--- Buffer Strategy - Store data when processing is slow

# The sample shows proper Overflow with BUFFER_NEW_ITEMS strategy
# (or :BUFFER for short).
# When the buffer is full, new items are stored temporarily until
# processing capacity becomes available to drain them.

pr()

Rs = new stzReactiveSystem()
Rs {

    # High-frequency data stream with slow processing

    oBufferStream = CreateStream("buffer-example")
    oBufferStream {
        # Set buffer strategy with small buffer for demo
        SetOverflowStrategy(BUFFER_NEW_ITEMS, 3) # Or simply :BUFFER

        # Slow processing Rfunction (simulated)
        OnPassed(func data {
            ? "ğŸ“Š Processing: " + data + " (slow processing)"
            # Simulate slow processing
        })
        
        OnOverflow(func(current, max) {
            ? "ğŸš¦ Overflow activated: " + current + "/" + max + " buffer full"
        })
        
        # Rapid data reception

        for i = 1 to 7  # Exceeds buffer size of 3
            ? "Receiving item " + i
            Recieve("Data-" + i)
        next
        
        ? "Processing buffer..."
        ProcessAnItemFromBuffer()

    }
    
    RunLoop()
    #--> (#NOTE I added comments to the output for clarity)
 
    # Phase 1: Filling buffer (capacity: 3)
    # -------------------------------------
    # Receiving item 1
    # Receiving item 2
    # Receiving item 3
    # 
    # Phase 2: Buffer full - Overflow stores new items
    # ------------------------------------------------
    # Receiving item 4
    #ğŸš¦ Overflow activated: 3/3 buffer full
    # âš ï¸ Overflow: Buffering data (buffer full: 3/3)
    # 
    # Receiving item 5
    #ğŸš¦ Overflow activated: 3/3 buffer full
    #âš ï¸ Overflow: Buffering data (buffer full: 3/3)
    # 
    # Receiving item 6
    #ğŸš¦ Overflow activated: 3/3 buffer full
    #âš ï¸ Overflow: Buffering data (buffer full: 3/3)
    # 
    # Receiving item 7
    #ğŸš¦ Overflow activated: 3/3 buffer full
    #âš ï¸ Overflow: Buffering data (buffer full: 3/3)
    # 
    # Phase 3: Manual buffer processing handles buffered items
    #---------------------------------------------------------
    # Processing buffer...
    # ğŸ“Š Processing: Data-1 (slow processing)
    # ğŸ“Š Processing: Data-2 (slow processing)
    # ğŸ“Š Processing: Data-3 (slow processing)

}

#NOTE

# Processing a buffer means executing Rfunctions on all stored items
# in the buffer. Items were temporarily held due to overflow,
# and processing releases them to Rfunctions for execution.

# In our example above:

# Items 1-3 fill the buffer (capacity: 3)
# Items 4-7 are stored by overflow strategy (buffer extends)
# Processing executes stored items through Rfunctions in order

# It's like a waiting room: people (data) wait when the
# service desk (Rfunction) can't handle the flow, and
# processing is like calling the waiting people forward.

pf()
# Executed in 0.95 second(s) in Ring 1.23

/*--- Drop Strategy - Discard data when overwhelmed

# The sample shows DROP_NEW_ITEMS behavior (or :DROP for short)
# Buffer fills to capacity (3), then excess sensor readings
# are discarded to prevent system overload - sacrifices data 
# completeness for stability

pr()

Rs = new stzReactiveSystem()
Rs {
    # Real-time sensor stream that can afford to lose some data
    oDropStream = CreateStreamXT("drop-example", :SENSOR)
    oDropStream {
        SetOverflowStrategy(DROP_NEW_ITEMS, 2) # Or simply :DROP
        
        Transform(func reading {
            return "Sensor-" + reading + "Â°C"
        })
        
        OnPassed(func temperature {
            ? "ğŸŒ¡ï¸ Current temp: " + temperature
        })
        
        OnOverflow(func(current, max) {
            ? "ğŸš¨ Sensor overloaded, discarding readings"
        })

        ? "Simulating high-frequency sensor readings..."
        
        # Simulate sensor burst
        sensorReadings = [23.5, 23.7, 24.1, 24.3, 24.5, 24.8, 25.0]
        for i = 1 to len(sensorReadings)
            ? "Reading: " + sensorReadings[i]
            Recieve(sensorReadings[i])
        next
        
        stats = OverflowStats()
        ? NL + "Final stats - Dropped: " + stats[:droppedCount] + " readings"

    }
    
    RunLoop()
    #--> (#NOTE I added some comments to clarify the output)
    # Simulating high-frequency sensor readings...
    # 
    # Phase 1: Buffer fills with initial readings (capacity: 2)
    # ---------------------------------------------------------
    # Reading: 23.50
    # Reading: 23.70
    # 
    # Phase 2: Drop strategy - excess readings discarded
    # --------------------------------------------------
    # Reading: 24.10
    # ğŸš¨ Sensor overloaded, discarding readings
    # âš ï¸ Overflow: Dropping data item (dropped so far: 1)
    # 
    # Reading: 24.30
    # ğŸš¨ Sensor overloaded, discarding readings
    # âš ï¸ Overflow: Dropping data item (dropped so far: 2)
    # 
    # Reading: 24.50
    # ğŸš¨ Sensor overloaded, discarding readings
    # âš ï¸ Overflow: Dropping data item (dropped so far: 3)
    # 
    # Reading: 24.80
    # ğŸš¨ Sensor overloaded, discarding readings
    # âš ï¸ Overflow: Dropping data item (dropped so far: 4)
    # 
    # Reading: 25
    # ğŸš¨ Sensor overloaded, discarding readings
    # âš ï¸ Overflow: Dropping data item (dropped so far: 5)

    # Result: Only first 2 readings kept, rest dropped
    # ------------------------------------------------
    # Final stats - Dropped: 5 readings

}

pf()
# Executed in 0.92 second(s) in Ring 1.23

/*--- Latest Strategy - Keep most recent data

# The sample shows ACCEPT_LATEST_DISCARD_OLD_ITEMS behavior
# (or :LATEST for short)
# Buffer maintains fixed size by discarding oldest data when new arrives
# Ensures most current information is preserved for time-sensitive applications

pr()

Rs = new stzReactiveSystem()
Rs {
    # Stock price stream - only latest price matters
    oLatestStream = CreateStreamXT("latest-price", :NETWORK)
    oLatestStream {
        SetOverflowStrategy(ACCEPT_LATEST_DISCARD_OLD_ITEMS, 2) # Or simply :LATEST
        
        Transform(func price {
            return "AAPL: $" + price
        })
        
        OnPassed(func stockPrice {
            ? "ğŸ’° " + stockPrice
        })
        
        OnOverflow(func(current, max) {
            ? "ğŸ“ˆ High trading volume - keeping latest prices only"
        })
        
        ? "=== Latest Strategy Demo ==="
        ? "Simulating rapid stock price updates..."
        
        # Rapid price updates
        prices = [150.25, 150.30, 150.15, 150.45, 150.60, 150.55]
        for i = 1 to len(prices)
            ? "Price update: $" + prices[i]
            Recieve(prices[i])
        next
        
        ProcessAnItemFromBuffer()

    }
    
    RunLoop()
    #--> (#NOTE I added some comments to clarify the output)
    # 
    # Simulating rapid stock price updates...
    # 
    # Phase 1: Buffer fills with initial price updates (capacity: 2)
    # --------------------------------------------------------------
    # Price update: $150.25
    # Price update: $150.30
    # 
    # Phase 2: Latest strategy - keeps newest prices, discards oldest
    # ---------------------------------------------------------------
    # Price update: $150.15
    # ğŸ“ˆ High trading volume - keeping latest prices only
    # âš ï¸ Overflow: Keeping latest, dropped oldest
    # 
    # Price update: $150.45
    # ğŸ“ˆ High trading volume - keeping latest prices only
    # âš ï¸ Overflow: Keeping latest, dropped oldest
    # 
    # Price update: $150.60
    # ğŸ“ˆ High trading volume - keeping latest prices only
    # âš ï¸ Overflow: Keeping latest, dropped oldest
    # 
    # Price update: $150.55
    # ğŸ“ˆ High trading volume - keeping latest prices only
    # âš ï¸ Overflow: Keeping latest, dropped oldest
    # 
    # Phase 3: Final buffer contains most recent prices
    # -------------------------------------------------
    # ğŸ’° AAPL: $150.60
    # ğŸ’° AAPL: $150.55
}

pf()
# Executed in 0.93 second(s) in Ring 1.23

/*--- Block Strategy - Pause stream when capacity reached

# The sample shows BLOCK_QUEUE_UNTIL_CAPACITY_AVAILABLE behavior
# (:BLOCK for short)
# Stream reception is paused when buffer fills to prevent data loss
# Maintains data integrity by synchronizing data flow with processing
# capacity

pr()

Rs = new stzReactiveSystem()
Rs {
    # Critical system events - cannot lose data
    oBlockStream = CreateStream("critical-events")
    oBlockStream {
        SetOverflowStrategy(BLOCK_QUEUE_UNTIL_CAPACITY_AVAILABLE, 3) # or simply :BLOCK
        
        Filter(func event {
            return event[:severity] = "CRITICAL"
        })
        
        OnPassed(func criticalEvent {
            ? "ğŸš¨ CRITICAL: " + criticalEvent[:message]
            # Simulate slow processing of critical events
        })
        
        OnOverflow(func(current, max) {
            ? "â›” System overload - pausing stream reception"
        })
        
        ? "Block Strategy Demo..."
        
        events = [
            [:severity = "CRITICAL", :message = "Database failure"],
            [:severity = "CRITICAL", :message = "Memory exhausted"], 
            [:severity = "INFO", :message = "User login"],
            [:severity = "CRITICAL", :message = "Security breach"],
            [:severity = "CRITICAL", :message = "Disk full"],
            [:severity = "CRITICAL", :message = "Network down"]
        ]
        
        for i = 1 to len(events)
            ? "Event: " + events[i][:severity] + " - " + events[i][:message]
            Recieve(events[i])
        next

    }
    
    RunLoop()
    #--> (#NOTE I added some comments to clarify the output)
    # Block Strategy Demo...
    # 
    # Phase 1: Buffer fills with initial events (capacity: 3)
    # -------------------------------------------------------
    # Event: CRITICAL - Database failure
    # Event: CRITICAL - Memory exhausted
    # Event: INFO - User login
    # 
    # Phase 2: Block strategy - stream reception paused to prevent overflow
    # ---------------------------------------------------------------------
    # Event: CRITICAL - Security breach
    # â›” System overload - pausing stream reception
    # âš ï¸ Overflow: Stream reception blocked (simulated)
    # 
    # Event: CRITICAL - Disk full
    # â›” System overload - pausing stream reception
    # âš ï¸ Overflow: Stream reception blocked (simulated)
    # 
    # Event: CRITICAL - Network down
    # â›” System overload - pausing stream reception
    # âš ï¸ Overflow: Stream reception blocked (simulated)
    # 
    # ~> No processing shown - events remain queued until
    # processing capacity becomes available
}

pf()
# Executed in 0.92 second(s) in Ring 1.23

/*--- Real-World Example: Log Processing with Adaptive Overflow
*/
# The sample shows adaptive overflow - system dynamically
# switches strategies, changing from buffer to drop mode under
# extreme load to maintain stability

# Processes existing buffer before applying new strategy

pr()

Rs = new stzReactiveSystem()
Rs {
    # Log processing system with smart overflow
    oLogStream = CreateStreamXT("adaptive-logs", OPTIMISED_FOR_FILE_SOURCE)
    oLogStream {
        # Start with buffer strategy
        SetOverflowStrategy(:BUFFER, 5)
        
        # Parse and enrich log entries
        Transform(func logLine {
            parts = split(logLine, "|")
            return [
                :timestamp = parts[1],
                :level = parts[2],
                :service = parts[3], 
                :message = parts[4],
                :processed_at = clocksPerSecond()
            ]
        })
        
        # Filter for important logs
        Filter(func log {
            importantLevels = ["ERROR", "WARN", "CRITICAL"]
            return find(importantLevels, log[:level])
        })
        
        OnPassed(func importantLog {
            ? "ğŸ“‹ " + importantLog[:level] + " [" + importantLog[:service] + "] " + importantLog[:message]
        })
        
        # Adaptive Overflow Rfunction
        OnOverflow(func(current, max) {
            ? "ğŸ”„ Log processing overflow: " + current + "/" + max
            
            # Switch to drop strategy under extreme load
            if current >= max
                ? "âš¡ Switching to DROP strategy due to extreme load"
                oLogStream.SetOverflowStrategy(:DROP, 10)
            ok
        })
        
        ? "Adaptive Log Processing Demo..."
        
        # Simulate log burst
        logEntries = [
            "2024-01-15 10:30:15|INFO|AUTH|User login successful",
            "2024-01-15 10:30:16|ERROR|DB|Connection timeout", 
            "2024-01-15 10:30:17|WARN|CACHE|High memory usage",
            "2024-01-15 10:30:18|INFO|API|Request processed",
            "2024-01-15 10:30:19|CRITICAL|SECURITY|Breach detected",
            "2024-01-15 10:30:20|ERROR|PAYMENT|Transaction failed",
            "2024-01-15 10:30:21|WARN|DISK|Low space warning",
            "2024-01-15 10:30:22|ERROR|NETWORK|Connection lost",
            "2024-01-15 10:30:23|CRITICAL|SYSTEM|Out of memory"
        ]
        
        RecieveMany(logEntries)
        
        ? "Processing remaining buffer..."
        ProcessAnItemFromBuffer()
        
        stats = OverflowStats() 
        ? "Final processing stats:"
        ? "  â€¢ Strategy: " + stats[:strategy]
        ? "  â€¢ Items dropped: " + stats[:droppedCount]
        ? "  â€¢ Buffer utilization: " + stats[:currentBuffer] + "/" + stats[:bufferSize]

    }
    
    RunLoop()
    #-->  (#NOTE I added some comments to clarify the output)
    # Adaptive Log Processing Demo...
    # 
    # Phase 1: Buffer reaches capacity, triggers strategy change
    # ----------------------------------------------------------
    # ğŸ”„ Log processing overflow: 5/5
    # âš¡ Switching to DROP strategy due to extreme load
    # âš ï¸ Overflow: Dropping data item (dropped so far: 1)
    # 
    # Phase 2: Process buffered logs before strategy switch
    # -----------------------------------------------------
    # Processing remaining buffer...
    # ğŸ“‹ ERROR [DB] Connection timeout
    # ğŸ“‹ WARN [CACHE] High memory usage
    # ğŸ“‹ CRITICAL [SECURITY] Breach detected
    # ğŸ“‹ WARN [DISK] Low space warning
    # ğŸ“‹ ERROR [NETWORK] Connection lost
    # ğŸ“‹ CRITICAL [SYSTEM] Out of memory
    # 
    # Phase 3: Results after adaptive processing
    # ------------------------------------------
    # Final processing stats:
    # - Strategy: drop
    # - Items dropped: 1
    # - Buffer utilization: 0/10
}

pf()
# Executed in 0.95 second(s) in Ring 1.23
